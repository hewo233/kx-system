// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM == 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to restart the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // Address of next instruction

    PARTS:
    Mux16(a=instruction, b=ALUout, sel=instruction[15], out=NewA);//ins[15]

    //A Register
    Not(in=instruction[15], out=AinsTrue);
    Or(a=AinsTrue, b=instruction[5], out=Achange);
    ARegister(in=NewA, load=Achange, out=Areg, out[0..14]=addressM);

    Mux16(a=Areg, b=inM, sel=instruction[12], out=ALUy);

    //D Register
    And(a=instruction[15], b=instruction[4], out=Dchange);
    DRegister(in=ALUout, load=Dchange, out=ALUx);
    
    //ALU
    And(a=instruction[15], b=instruction[11], out=ALUzx);
    And(a=instruction[15], b=instruction[10], out=ALUnx);
    Or(a=AinsTrue, b=instruction[9], out=ALUzy);
    Or(a=AinsTrue, b=instruction[8], out=ALUny);
    And(a=instruction[15], b=instruction[7], out=ALUf);
    And(a=instruction[15], b=instruction[6], out=ALUno);
    
    ALU(x=ALUx, y=ALUy, zx=ALUzx, nx=ALUnx, zy=ALUzy, ny=ALUny, f=ALUf, no=ALUno, out=ALUout, out=outM, zr=zr, ng=ng);
    //writeM
    And(a=instruction[15], b=instruction[3], out=writeM);

    //PC
    Or(a=zr, b=ng, out=notByzr);//<=0
    Not(in=notByzr, out=byzr);//>0

    And(a=byzr, b=instruction[0], out=j1);
    And(a=zr, b=instruction[1], out=j2);
    And(a=ng, b=instruction[2], out=j3);

    Or(a=j1, b=j2, out=j12);
    Or(a=j12, b=j3, out=j123);

    And(a=j123, b=instruction[15], out=jump);

    PC(in=Areg, inc=true, load=jump, reset=reset, out[0..14]=pc);
}